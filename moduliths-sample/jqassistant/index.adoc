= Modulith

== Overview

include::jQA:Summary[]

[[default]]
[role=group,includesConcepts="modulith:ModuleDependencies,modulith:ModuleExposesType"]
== Reports

[[modulith:ModulithApplication]]
[source,cypher,role=concept]
.Classes annotated by `org.moduliths.Modulith` are labeled with `Modulith` and `Application`.
----
MATCH
  (:Artifact)-[:CONTAINS]->(modulith:Type)-[:ANNOTATED_BY]->()-[:OF_TYPE]->(:Type{fqn:"org.moduliths.Modulith"})
SET
  modulith:Modulith:Application
RETURN
  modulith as Modulith
----

[[modulith:Module]]
[source,cypher,role=concept,requiresConcepts="modulith:ModulithApplication"]
.Each package that is located within the same package as the Modulith application class is labeled with `Module`.
----
MATCH
  (root:Package)-[:CONTAINS]->(modulith:Modulith:Application),
  (root)-[:CONTAINS]->(module:Package)
OPTIONAL MATCH
  (module)-[:CONTAINS]->(:Type{name:"package-info"})-[:ANNOTATED_BY]->(moduleInfo),
  (moduleInfo)-[:OF_TYPE]->(:Type{fqn:"org.moduliths.Module"}),
  (moduleInfo)-[:HAS]->(displayName:Value{name:"displayName"})
SET
  module:Module
SET
  module.displayName = coalesce(displayName.value, module.name)
RETURN
  module.displayName as Module
ORDER BY
  Module
----

[[modulith:ModuleDependencies]]
[source,cypher,role=concept,requiresConcepts="modulith:Module",reportType="plantuml-component-diagram"]
.A dependency between two modules exists if there's a type dependency between both. The module dependency is represented by `DEPENDS_ON_MODULE` relationships having a  `weight` property indicating the degree of coupling.
----
MATCH
  (module1:Module)-[:CONTAINS*]->(type1:Type),
  (module2:Module)-[:CONTAINS*]->(type2:Type),
  (type1)-[dependsOn:DEPENDS_ON]->(type2)
WHERE
  module1 <> module2
WITH
  module1, module2, count(dependsOn) as weight
MERGE
  (module1)-[dependsOnModule:DEPENDS_ON_MODULE]->(module2)
SET
  dependsOnModule.weight = weight
RETURN
  module1, dependsOnModule, module2
----

[[modulith:ModuleExposesType]]
[source,cypher,role=concept,requiresConcepts="modulith:ModuleDependencies"]
.A type of a module is exposed if it is referenced at least once by a type in another module.
----
MATCH
  (module:Module)
OPTIONAL MATCH
  (dependent:Module)-[:DEPENDS_ON_MODULE]->(module),
  (dependent)-[:CONTAINS*]->(dependentType:Type),
  (module)-[:CONTAINS*]->(type:Type),
  (dependentType)-[:DEPENDS_ON]->(type)
RETURN
  module.displayName as Module, collect(type.fqn) as ExposedTypes
ORDER BY
  Module
----
