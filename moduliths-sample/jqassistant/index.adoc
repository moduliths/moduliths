= Modulith

[[default]]
[role=group,includesConcepts="modulith:ModuleDependencies"]
== Default Reports

[[modulith:ModulithApplication]]
[source,cypher,role=concept]
.Classes annotated by `de.olivergierke.moduliths.Modulith` are labeled with `Modulith` and `Application`.
----
MATCH
  (:Artifact)-[:CONTAINS]->(modulith:Type)-[:ANNOTATED_BY]->()-[:OF_TYPE]->(:Type{fqn:"de.olivergierke.moduliths.Modulith"})
SET
  modulith:Modulith:Application
RETURN
  modulith as Modulith
----

[[modulith:Module]]
[source,cypher,role=concept,requiresConcepts="modulith:ModulithApplication"]
.Each package that is located within same package as the Modulith application class is labeled with `Module`.
----
MATCH
  (root:Package)-[:CONTAINS]->(modulith:Modulith:Application),
  (root)-[:CONTAINS]->(module:Package)
OPTIONAL MATCH
  (module)-[:CONTAINS]->(:Type{name:"package-info"})-[:ANNOTATED_BY]->(moduleInfo),
  (moduleInfo)-[:OF_TYPE]->(:Type{fqn:"de.olivergierke.moduliths.Module"}),
  (moduleInfo)-[:HAS]->(displayName:Value{name:"displayName"})
SET
  module:Module
SET
  module.displayName = coalesce(displayName.value, module.name)
RETURN
  module.displayName as Module
ORDER BY
  Module
----

[[modulith:ModuleDependencies]]
[source,cypher,role=concept,requiresConcepts="modulith:Module",reportType="plantuml-component-diagram"]
.A dependency between two modules exists if there's a type dependency between both. The module dependency is represented by `DEPENDS_ON_MODULE` relationships having a  `weight` property indicating the degree of coupling.
----
MATCH
  (module1:Module)-[:CONTAINS*]->(type1:Type),
  (module2:Module)-[:CONTAINS*]->(type2:Type),
  (type1)-[dependsOn:DEPENDS_ON]->(type2)
WHERE
  module1 <> module2
WITH
  module1, module2, count(dependsOn) as weight
MERGE
  (module1)-[dependsOnModule:DEPENDS_ON_MODULE]->(module2)
SET
  dependsOnModule.weight = weight
RETURN
  module1, dependsOnModule, module2
----

